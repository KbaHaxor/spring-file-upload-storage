subprojects {
  group = 'nl.runnable.spring-file-upload-storage'
  version = '0.0.3'

  apply plugin: 'java'
  apply plugin: 'groovy'
  apply plugin: 'maven-publish'

  sourceCompatibility = 1.6
  targetCompatibility = 1.6

  ext {
    h2Version = '1.3.176'
    jetBrainsAnnotationsVersion = '13.0'
    spockVersion = '0.7-groovy-2.0'
    springFrameworkVersion = '4.1.5.RELEASE'
    slf4jVersion = '1.7.10'
    tomcatJdbcVersion = '8.0.20'
    logbackVersion = '1.1.2'
  }

  dependencies {
    compile "org.springframework:spring-web:${springFrameworkVersion}"
    compile "org.springframework:spring-jdbc:${springFrameworkVersion}"
    compile "org.slf4j:slf4j-api:${slf4jVersion}"
    compile "org.jetbrains:annotations:${jetBrainsAnnotationsVersion}"
    testCompile "org.spockframework:spock-spring:${spockVersion}"
    testCompile "org.springframework:spring-test:${springFrameworkVersion}"
    testCompile "com.h2database:h2:${h2Version}"
    testRuntime "ch.qos.logback:logback-classic:${logbackVersion}"
  }

  repositories {
    jcenter()
  }

  task sourceJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
  }

  publishing {
    publications {
      mavenJava(MavenPublication) {
        from components.java
        artifact sourceJar {
          classifier "sources"
        }
      }
    }
    repositories {
      if (project.hasProperty('PUBLISH_LOCAL')) {
        mavenLocal()
      } else {
        maven {
          url 'https://api.bintray.com/maven/lfridael/maven/spring-file-upload-storage'
          credentials {
            username = project.hasProperty('BINTRAY_USERNAME') ? project.BINTRAY_USERNAME : ''
            password = project.hasProperty('BINTRAY_API_KEY') ? project.BINTRAY_API_KEY : ''
          }
        }
      }
    }
  }

}

defaultTasks 'build'

task wrapper(type: Wrapper) {
  gradleVersion = '2.2.1'
}
